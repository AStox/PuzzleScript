title Block Faker
author Droqen
homepage www.droqen.com

require_player_movement
key_repeat_interval 0.12

background_color black
text_color white

color_palette c64
(
A port of first several levels of Droqen's beautiful game:

http://dl.dropboxusercontent.com/u/4452540/locklake/index.html

(his game has secrets that this one does not)

)

========
OBJECTS
========

Grille
white
0......0
.0....0.
..0..0..
...00...
...00...
..0..0..
.0....0.
0......0

EndPoint
white
........
........
..0..0..
........
........
..0..0..
........
........

Player
white black
........
.000000.
.000000.
.010010.
.010010.
.000000.
.000000.
........

Wall
white

GreenBlock
white
........
.000000.
.000000.
.00..00.
.00..00.
.000000.
.000000.
........

PinkBlock
white
........
.000000.
.00..00.
.0....0.
.0....0.
.00..00.
.000000.
........

PurpleBlock
white
........
.000000.
.0.00.0.
.000000.
.000000.
.0.00.0.
.000000.
........

OrangeBlock
white
........
.000000.
.0....0.
.0.00.0.
.0.00.0.
.0....0.
.000000.
........


=======
LEGEND
=======

Block = PinkBlock or PurpleBlock or OrangeBlock or GreenBlock
Moveable = Player or Block
PlayerObstacle = Block or Wall 
BlockObstacle = Player or Wall  or Grille or Block or EndPoint
# = Wall
@ = Wall
P = Player
G = GreenBLock
K = PinkBlock
R = PurpleBlock
O = OrangeBlock
E = EndPoint
x = Grille
H = Grille and Player


=========
SOUNDS
=========

================
COLLISIONLAYERS
================

EndPoint
Grille
Player, Wall, PinkBlock, PurpleBlock, OrangeBlock, GreenBlock

======
RULES
======

[ >  Moveable | Moveable ] -> [ > Moveable | > Moveable ]

[ > Block | Grille ] -> [ Block | Grille ]

late [ PinkBlock | PinkBlock | PinkBlock ] -> [ | | ]
late [ PurpleBlock | PurpleBlock | PurpleBlock ] -> [ | | ]
late [ OrangeBlock | OrangeBlock | OrangeBlock ] -> [ | | ]
late [ GreenBlock | GreenBlock | GreenBlock ] -> [ | | ]

==============
WINCONDITIONS
==============
some player on endpoint
=======
LEVELS
=======

########@@@@@###
####@@@@@...@###
###@..OO....@@##
##@..@@@@@....@#
##@.@..@@@..E@@#
#@..@.O...@@@@##
##@...@.P.######
####@.@@@@######

##########
####R.R###
#x....#x##
#xO#O.OxE#
#xx##.#xx#
#####R####
#####P####
O#########

.....##P.#####..
....###....####.
..#####O...#R..#
..#####.#.##RR.#
##ERR..O.O.....#
..#####.#.#....#
..######..#G.###
....####.GG.OOx.

