title Match 3 Block Push
author Stephen Lavelle
homepage www.puzzlescript.net

========
OBJECTS
========



Target
white
........
........
........
...0....
....0...
........
........
........

Wall
white
00000000
00000000
00000000
00000000
00000000
00000000
00000000
00000000

Player
white 
........
..0000..
..0000..
.000000.
..0000..
..0..0..
.00..00.
........

Crate
white
........
.000000.
.0....0.
.0....0.
.0....0.
.0....0.
.000000.
........




=======
LEGEND
=======

# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Target
Player, Wall, Crate

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

late [ Crate | Crate | Crate ] -> [ | |]

==============
WINCONDITIONS
==============

All Crate on Target

=======
LEVELS
=======

#########
#.......#
#.*.*.@.#
#.P...O.#
#...*.*.#
#.......#
#########

.#######.
.#.....#.
.#O##..##
##.##.*.#
#.*..**.#
#.OOO.#.#
#.P.....#
#########

